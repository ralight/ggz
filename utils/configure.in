dnl Process this file with autoconf to produce a configure script.

AC_INIT(agrue/agrue.c)
AM_INIT_AUTOMAKE(ggz-utils, 0.0.5pre)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

dnl Compiling Options
dnl =====================
AC_ARG_ENABLE(agrue,  [  --disable-agrue        Don't build AGRUE],, enable_agrue=yes)
AC_ARG_ENABLE(grubby, [  --disable-grubby       Don't build GRUBBY],, enable_grubby=yes)
AC_ARG_ENABLE(gensec, [  --disable-gensec       Don't build GenSecure],, enable_gensec=yes)
AC_ARG_ENABLE(telggz, [  --disable-telggz       Dont' build TelGGZ],, enable_telggz=yes)
AC_ARG_ENABLE(metaserv, [  --disable-metaserv     Dont' build MetaServ],, enable_metaserv=yes)
AC_ARG_ENABLE(debug,  [  --disable-debug         Turn off debugging (on by default)],, enable_debug=yes)


dnl Check for standard build environment
dnl ====================================
AC_PROG_CC
AC_ISC_POSIX
AC_PROG_INSTALL
AC_PROG_CPP
AM_PROG_CC_STDC
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL

dnl Check for header files
dnl ======================
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(sys/time.h unistd.h)
AC_CHECK_HEADERS(openssl/ssl.h, , enable_gensec=no)


dnl Check for typedefs, structures, and compiler characteristics
dnl ============================================================
AC_C_CONST
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL
AC_HEADER_TIME


dnl Check for library functions
dnl ===========================
AC_CHECK_FUNC(select)
AC_CHECK_FUNC(socket, ,
    AC_CHECK_LIB(socket, socket,
	AC_CHECK_HEADER(sys/socket.h, LDADD="-lsocket",
	    AC_MSG_ERROR(*** Cannot find socket headers ); exit ),
	AC_MSG_ERROR(*** Cannot find socket library ); exit ))

AC_CHECK_FUNC(gethostbyname, ,
    AC_CHECK_LIB(nsl, gethostbyname, LDADD="$LDADD -lnsl",
	AC_MSG_ERROR(*** Cannot find nsl library ); exit ))

AC_CHECK_FUNC(inet_ntoa, ,
    AC_CHECK_LIB(nsl, inet_ntoa, LDADD="$LDADD -lnsl",
	AC_MSG_ERROR(*** Cannot find nsl library ); exit ))

AC_CHECK_LIB(popt, poptGetNextOpt,
    AC_CHECK_HEADER(popt.h, grubby_LIBS="$grubby_LIBS -lpopt",
        AC_MSG_ERROR(*** Cannot find popt.h header); exit),
    AC_MSG_ERROR(*** Cannot find popt library ); exit)

AC_CHECK_LIB(easysock, es_writen,
    AC_CHECK_HEADER(easysock.h, LDADD="$LDADD -leasysock",
        AC_MSG_ERROR(*** Cannot find easysock.h header); exit),
    AC_MSG_ERROR(*** Cannot find easysock library ); exit)
		           

dnl Use -Wall if we have gcc.
dnl ========================
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl


dnl Debug modes
dnl ===========
if test "$enable_debug" = yes; then
    AC_DEFINE(DEBUG)
    DFLAGS="DEBUG"
fi

if test "$enable_debug_socket" = yes; then
    AC_DEFINE(DEBUG_SOCKET)
    DFLAGS="$DFLAGS DEBUG_SOCKET"
fi

if test "$enable_debug_mem" = yes; then
    AC_DEFINE(DEBUG_MEM)
    DFLAGS="$DFLAGS DEBUG_MEM"
    LDADD="$LDADD -ldmalloc"
fi

if test "$enable_debug_gdb" = yes; then
    CFLAGS="$CFLAGS -ggdb"
fi


dnl Which progs to compile
dnl ======================

SUBDIRS="man"

if test "$enable_agrue" = yes; then
    SUBDIRS="$SUBDIRS agrue"
fi

if test "$enable_grubby" = yes; then
    SUBDIRS="$SUBDIRS grubby"
fi

if test "$enable_gensec" = yes; then
	SUBDIRS="$SUBDIRS gensecure"
fi

if test "$enable_metaserv" = yes; then
	SUBDIRS="$SUBDIRS metaserv"
	if test "$enable_telggz" = yes; then
		SUBDIRS="$SUBDIRS telggz"
	fi
fi

AC_SUBST(LDADD)
AC_SUBST(grubby_LIBS)
AC_SUBST(SUBDIRS)

AC_OUTPUT(Makefile
		grubby/Makefile
		agrue/Makefile
		gensecure/Makefile
		gensecure/io/Makefile
		gensecure/src/Makefile
		telggz/Makefile
		metaserv/Makefile
		metaserv/minidom/Makefile
		metaserv/metaserv/Makefile
		man/Makefile)

dnl Output Status
dnl =============

echo "AGRUE.......$enable_agrue"
echo "Grubby......$enable_grubby"
echo "GenSecure...$enable_gensec"
echo "TelGGZ......$enable_telggz"
echo "MetaServ....$enable_metaserv"
echo "Debug.......$enable_debug"

