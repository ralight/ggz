dnl Configuration for KGGZ
dnl ======================

AC_INIT(acinclude.m4)
AC_CANONICAL_SYSTEM
AC_ARG_PROGRAM
AM_INIT_AUTOMAKE(ggz-kde-client,0.0.4)
AM_CONFIG_HEADER(config.h)

dnl Options for building KGGZ
dnl =========================

AC_ARG_ENABLE(ggzcorepp, [  --disable-ggzcorepp  don't build GGZCore++             ], enable_ggzcorepp=no, enable_ggzcorepp=yes)
AC_ARG_ENABLE(browser,   [  --enable-browser     include KHTML browser [default=no]], enable_browser=yes, enable_browser=no)

dnl Check for Qt and KDE
dnl ====================

KDE_SET_PREFIX
KDE_USE_QT(2)
AC_CHECK_COMPILERS
AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)
KDE_PROG_LIBTOOL
AM_PROG_LIBTOOL
AM_KDE_WITH_NLS
AC_PATH_KDE

SUBDIRS="src data po"

dnl Check for easysock and ggzcore
dnl ==============================

if test "$enable_ggzcorepp" = yes; then
	AC_GGZ_EASYSOCK
	AC_GGZ_GGZCORE
	SUBDIRS="ggzcore++ $SUBDIRS"
fi

dnl Set up variables for KGGZ
dnl =========================

if test "$enable_browser" = yes; then
	kggzbrowservar=1;
	BROWSERSUBDIR="more";
	BROWSERLIB="-L widgets/more -lkggzuimore -lkhtml";
	AC_DEFINE_UNQUOTED(KGGZ_BROWSER, ${kggzbrowservar})
else
	kggzbrowservar=;
	BROWSERSUBDIR="";
	BROWSERLIB="";
fi

AC_DEFINE_UNQUOTED(KGGZ_DIRECTORY, "${kde_datadir_c}/kggz")
dnl AC_DEFINE_UNQUOTED(KGGZ_BROWSER, ${kggzbrowservar})

AC_SUBST(SUBDIRS)
AC_SUBST(BROWSERSUBDIR)
AC_SUBST(BROWSERLIB)

dnl Create Makefiles
dnl ================

AC_OUTPUT(Makefile
	modsniff/Makefile
	ggzap/Makefile
	ggz-kde/Makefile
	ggz-kde/ggzcore++/Makefile
	ggz-kde/ggzcore++/src/Makefile
	ggz-kde/ggzcore++/doc/Makefile
	ggz-kde/ggzcore++/doc/html/Makefile
	ggz-kde/src/Makefile
	ggz-kde/src/widgets/Makefile
	ggz-kde/src/widgets/more/Makefile
	ggz-kde/src/abstract/Makefile
	ggz-kde/data/Makefile
	ggz-kde/data/browser/Makefile
	ggz-kde/data/icons/Makefile
	ggz-kde/data/icons/games/Makefile
	ggz-kde/data/kde/Makefile
	ggz-kde/po/Makefile)

dnl Report success
dnl ==============

echo ""
echo "KGGZ is now configured. Please type 'make' in order to compile it."
echo "The following installation layout is used:"
echo ""
echo "Installation prefix: $prefix"
echo "Data: $prefix/share/apps/kggz/"
echo "Build included GGZCore++: $enable_ggzcorepp"
echo "Build optional KGGZBrowser: $enable_browser"
echo ""

